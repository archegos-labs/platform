provider "flux" {
  kubernetes = {
    host                   = var.cluster_endpoint
    cluster_ca_certificate = base64decode(var.cluster_certificate_authority_data)
    exec = {
      api_version = "client.authentication.k8s.io/v1beta1"
      args        = ["eks", "get-token", "--cluster-name", var.cluster_name]
      command     = "aws"
    }
  }

  git = {
    url = "ssh://git@github.com/${var.github_org}/${var.github_repository}.git"
    ssh = {
      username    = "git"
      private_key = tls_private_key.flux.private_key_pem
    }
  }
}

provider "github" {
  owner = var.github_org
  token = var.github_pat
}

resource "github_repository" "fluxcd" {
  name        = var.github_repository
  description = "The home of Flux app definitions"

  visibility = "public"
  license_template = "mit"
  gitignore_template = "Terraform"
  has_issues = true

  lifecycle {
    prevent_destroy = true
  }
}

resource "tls_private_key" "flux" {
  algorithm   = "ECDSA"
  ecdsa_curve = "P256"
}

resource "github_repository_deploy_key" "this" {
  title      = "Flux"
  repository = github_repository.fluxcd.name
  key        = tls_private_key.flux.public_key_openssh
  read_only  = "false"
}

resource "flux_bootstrap_git" "this" {
  depends_on = [github_repository_deploy_key.this]

  components_extra = [
    "image-reflector-controller",
    "image-automation-controller"
  ]
  embedded_manifests = true
  path               = "clusters/${var.cluster_name}"
}
